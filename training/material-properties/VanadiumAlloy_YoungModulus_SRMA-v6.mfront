@DSL MaterialLaw;
@Material VanadiumAlloy;
@Law YoungModulus_SRMA2008;
@Author T.Helfer;
@Date 2008 - 11 - 17;

@Description {
  Propriétés et comportement mécanique d alliages
    de Vanadium avant, après et sous irradiation

  Marion Le Flem, Jean-Luc Bechade, Annick Bougault,
  Aurore Michaux, Lionel Gosmain, Jean-Louis Seran
  DMN/SRMA/LA2M/NT/2008-2967/A
}

@UseQt true;

@Output stress E;
E.setGlossaryName("YoungModulus");

@Input temperature TK;
TK.setGlossaryName("Temperature");

@PhysicalBounds TK in [0:*[;
@Bounds TK in [293.13:973.15];

@Parameter stress E0 = 127.8e9;
E0.setEntryName("ReferenceYoungModulus");
@Parameter invert_type<temperature> alpha = 7.825e-5;
alpha.setEntryName("TemperatureDependency");

@Function {
  constexpr auto Tref = temperature{293.15};
  E = E0 * (1 - alpha * (TK - Tref));
}
